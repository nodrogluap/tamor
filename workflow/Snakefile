#!/usr/bin/env -S snakemake --use-conda --quiet --snakefile

import os.path
import csv
from pathlib import Path
import glob

from snakemake.utils import validate
from scripts.tamor_utils import decomment

# Site-specific config, most changes would be made in that file rather than this one.
configfile: "config/config.yaml"

# Automated reports of results as a single HTML page
report: "report/workflow.rst"

# Roughly in order of rule execution
include: "rules/resources.smk"
include: "rules/metadata.smk"
include: "rules/fastq_list.smk"
include: "rules/bcl_conversion.smk"
include: "rules/bams.smk"
include: "rules/rna.smk"
include: "rules/dna.smk"
include: "rules/karyoploter.smk"
include: "rules/pcgr.smk"
include: "rules/djerba.smk"
include: "rules/metrics.smk"
include: "rules/datavzrd.smk"
include: "rules/qiagen_ipa.smk"

# read and validate the provided dna sample config file
dna_sample_config_tsv = (
    pd.read_csv(config["dna_paired_samples_tsv"], sep="\t", 
	dtype={"subjectID": str, "tumorSampleID": str, "germlineSampleID": str, "projectID": str},
	comment='#').set_index(["tumorSampleID"], drop=False)
)
validate(dna_sample_config_tsv, schema="schemas/dna_sample_config.schema.yaml")

# read and validate the provided rna sample config file
rna_sample_config_tsv = (
    pd.read_csv(config["rna_paired_samples_tsv"], sep="\t", 
	dtype={"subjectID": str, "tumorRNASampleID": str, "projectID": str},
	comment='#').set_index(["tumorRNASampleID"], drop=False)
)
validate(rna_sample_config_tsv, schema="schemas/rna_sample_config.schema.yaml")


rule all:
	# Download reference data if not found on the system.
	# Generate a Dragen somatic mutation analysis for each pair for tumor-normal listed in the DNA pairs file.
	# Generate RNA quantification for samples listed in the RNA samples file.
	# Also generate a PCGR user-friendly variant report, which will require the germline variant calls as well in another rule.
	input:
		config["ref_fasta"],
		expand(config["output_dir"]+"/{project}/{subject}/rna/{subject}_{rna}.rna.quant.genes.hugo.tpm.txt", zip, project=rna_sample_config_tsv["projectID"], subject=rna_sample_config_tsv["subjectID"], rna=rna_sample_config_tsv["tumorRNASampleID"]),
		expand(config["output_dir"]+"/{project}/{subject}/rna/{subject}_{rna}.rna.quant.genes.fpkm.txt", zip, project=rna_sample_config_tsv["projectID"], subject=rna_sample_config_tsv["subjectID"], rna=rna_sample_config_tsv["tumorRNASampleID"]),
		expand(config["output_dir"]+"/{project}/{subject}/rna/{subject}_{rna}.rna.fusion_candidates.features.csv", zip, project=rna_sample_config_tsv["projectID"], subject=rna_sample_config_tsv["subjectID"], rna=rna_sample_config_tsv["tumorRNASampleID"]),
		expand(config["output_dir"]+"/{project}/{subject}/{subject}_{tumor}_{normal}.dna.somatic.hard-filtered.vcf.gz", zip, project=dna_sample_config_tsv["projectID"], subject=dna_sample_config_tsv["subjectID"], tumor=dna_sample_config_tsv["tumorSampleID"], normal=dna_sample_config_tsv["germlineSampleID"]),
		expand(config["output_dir"]+"/{project}/{subject}/{subject}_{tumor}_{normal}.dna.somatic.cnv.vcf.gz", zip, project=dna_sample_config_tsv["projectID"], subject=dna_sample_config_tsv["subjectID"], tumor=dna_sample_config_tsv["tumorSampleID"], normal=dna_sample_config_tsv["germlineSampleID"]),
		expand(config["output_dir"]+"/{project}/{subject}/{subject}_{tumor}_{normal}.dna.somatic.sv.vcf.gz", zip, project=dna_sample_config_tsv["projectID"], subject=dna_sample_config_tsv["subjectID"], tumor=dna_sample_config_tsv["tumorSampleID"], normal=dna_sample_config_tsv["germlineSampleID"]),
		#expand(config["output_dir"]+"/{project_name}/{subject}/{subject}_{tumor}_{normal}.dna.somatic.qiagen-ipa.submitted.txt", zip, project_name=dna_sample_config_tsv["projectID"], subject=dna_sample_config_tsv["subjectID"], tumor=dna_sample_config_tsv["tumorSampleID"], normal=dna_sample_config_tsv["germlineSampleID"]),
		expand(config["output_dir"]+"/{project}/{subject}/{subject}_{tumor}_{normal}.cnv_plot.jpeg", zip, project=dna_sample_config_tsv["projectID"], subject=dna_sample_config_tsv["subjectID"], tumor=dna_sample_config_tsv["tumorSampleID"], normal=dna_sample_config_tsv["germlineSampleID"]),
		expand(config["output_dir"]+"/{project}/{subject}/{subject}_{tumor}_{normal}.dna.somatic.sv.fusion_candidates.features.csv", zip, project=dna_sample_config_tsv["projectID"], subject=dna_sample_config_tsv["subjectID"], tumor=dna_sample_config_tsv["tumorSampleID"], normal=dna_sample_config_tsv["germlineSampleID"]),
		expand(config["output_dir"]+"/pcgr/{project}/{subject}_{tumor}_{normal}/{subject}.pcgr.grch38.html", zip, project=dna_sample_config_tsv["projectID"], subject=dna_sample_config_tsv["subjectID"], tumor=dna_sample_config_tsv["tumorSampleID"], normal=dna_sample_config_tsv["germlineSampleID"]),
		expand(config["output_dir"]+"/djerba/{project}/{subject}_{tumor}_{normal}/{subject}-v1_report.research.html", zip, project=dna_sample_config_tsv["projectID"], subject=dna_sample_config_tsv["subjectID"], tumor=dna_sample_config_tsv["tumorSampleID"], normal=dna_sample_config_tsv["germlineSampleID"])
